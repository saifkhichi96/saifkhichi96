apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

def secretPropertiesFile = rootProject.file("secret.properties")
def secretProperties = new Properties()
secretProperties.load(new FileInputStream(secretPropertiesFile))

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.saifkhichi.app"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 1
        versionName "1.0.0"

        // Read API secrets
        buildConfigField("String", "STRIPE_PUBLIC_KEY", secretProperties['STRIPE_PUBLIC_KEY'])
        buildConfigField("String", "STRIPE_SECRET_KEY", secretProperties['STRIPE_SECRET_KEY'])
    }

    // Enable view binding
    buildFeatures.viewBinding = true

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.activity:activity-ktx:1.3.0-rc01'  // for 'by viewModels'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation 'com.google.android.material:material:1.4.0'

    // Kotlin language
    implementation "androidx.core:core-ktx:$kotlin_version"

    // Kotlin coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:$kotlin_coroutines_version"

    // Lifecycle-aware components
    def lifecycle_version = "2.3.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version" // for 'LifecycleScope'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    // Firebase services
    implementation platform('com.google.firebase:firebase-bom:28.2.0')
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-database-ktx'
    implementation 'com.google.firebase:firebase-storage-ktx'

    // Dependency injection with Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    // SharedPreferences management with Hawk
    implementation 'com.orhanobut:hawk:2.0.1'

    implementation project(path: ':books')
    implementation project(path: ':database')
    implementation project(path: ':storage')

    // Stripe Android SDK for Payments
    implementation "com.stripe:stripe-java:20.50.0"
    implementation 'com.stripe:stripe-android:16.8.0'
}

kapt {
    correctErrorTypes true
}