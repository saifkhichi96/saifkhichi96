plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
}

def secretPropertiesFile = rootProject.file("secret.properties")
def secretProperties = new Properties()
secretProperties.load(new FileInputStream(secretPropertiesFile))

android {
    compileSdkVersion 32
    defaultConfig {
        applicationId "com.saifkhichi.app"
        minSdkVersion 24
        targetSdkVersion 32
        versionCode 1
        versionName "1.0.0"

        // Read API secrets
        buildConfigField("String", "STRIPE_PUBLIC_KEY", secretProperties['STRIPE_PUBLIC_KEY'])
        buildConfigField("String", "STRIPE_SECRET_KEY", secretProperties['STRIPE_SECRET_KEY'])
    }

    // Enable view binding
    buildFeatures.viewBinding = true

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "androidx.activity:activity-ktx:1.4.0"  // for 'by viewModels'
    implementation "androidx.appcompat:appcompat:1.4.1"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "com.google.android.material:material:1.5.0"

    // Kotlin language
    implementation "androidx.core:core-ktx:$kotlin_version"

    // Kotlin coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.6.0-native-mt"

    // Lifecycle-aware components
    def lifecycle_version = "2.4.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version" // for 'LifecycleScope'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    // Firebase services
    implementation platform("com.google.firebase:firebase-bom:29.0.4")
    implementation "com.google.firebase:firebase-auth-ktx"
    implementation "com.google.firebase:firebase-database-ktx"
    implementation "com.google.firebase:firebase-storage-ktx"

    // Dependency injection with Hilt
    implementation "com.google.dagger:hilt-android:2.40.5"
    kapt "com.google.dagger:hilt-android-compiler:2.40.5"

    // SharedPreferences management with Hawk
    implementation "com.orhanobut:hawk:2.0.1"

    // Stripe Android SDK for Payments
    implementation 'com.stripe:stripe-java:20.98.0'
    implementation 'com.stripe:stripe-android:19.1.0'

    implementation project(path: ':books')
    implementation project(path: ':database')
    implementation project(path: ':storage')
}

kapt {
    correctErrorTypes true
}